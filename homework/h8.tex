% Created 2020-11-25 Wed 11:19
% Intended LaTeX compiler: lualatex
\documentclass[11pt]{article}
\usepackage{graphicx}
\usepackage{grffile}
\usepackage{longtable}
\usepackage{wrapfig}
\usepackage{rotating}
\usepackage[normalem]{ulem}
\usepackage{amsmath}
\usepackage{textcomp}
\usepackage{amssymb}
\usepackage{capt-of}
\usepackage{hyperref}
\usepackage{tabularx}
\usepackage{etoolbox}
\makeatletter
\def\dontdofcolorbox{\renewcommand\fcolorbox[4][]{##4}}
\AtBeginEnvironment{minted}{\dontdofcolorbox}
\makeatother
\usepackage[newfloat]{minted}
\author{Mark Armstrong}
\date{November 19th, 2020}
\title{Computer Science 3MI3 – 2020 homework 8\\\medskip
\large Visualising the call stack in Clojure}
\hypersetup{
   pdfauthor={Mark Armstrong},
   pdftitle={Computer Science 3MI3 – 2020 homework 8},
   pdfkeywords={},
   pdfsubject={Understanding tail recursion by defining recursive functions in Clojure which print a representation of the call stack.},
   pdfcreator={Emacs 27.0.91 (Org mode 9.4)},
   pdflang={English},
   colorlinks,
   linkcolor=blue,
   citecolor=blue,
   urlcolor=blue
   }
\begin{document}

\maketitle
\tableofcontents


\section*{Introduction}
\label{sec:orgbff23ec}
In this homework, we provide you with a new Clojure special form
which can be used to create functions
which automatically output a visualisation of their calling context,
and hence their stack usage, as they run.
You are tasked with using this new special form to implement
several simple recursive functions both in a “naive” way
and a tail recursive way.

See \hyperref[sec:orgce45ffb]{Part 0.1} for an overview of “naive” and tail recursion.

\section*{Boilerplate}
\label{sec:org08fd026}
\subsection*{Submission procedures}
\label{sec:org8209664}
\subsubsection*{Submission method}
\label{sec:orgce13840}

Homework should be submitted to your McMaster CAS Gitlab respository
in the \texttt{cs3mi3-fall2020} project.

Ensure that you have \textbf{pushed} the commits to the remote repository
in time for the deadline, and not just committed to your local copy.

\subsubsection*{Naming requirements}
\label{sec:org9a949eb}

Place all files for the homework
inside a folder titled \texttt{hn}, where \texttt{n} is the number of the homework.
So, for homework 1, use the folder \texttt{h1}, for homework 2 the folder \texttt{h2}, etc.
Ensure you do not capitalise the \texttt{h}.

Unless otherwise instructed in the homework questions,
place all of your code for the homework
in a single file in the \texttt{hn} folder named \texttt{hn.ext},
where \texttt{ext} is the appropriate extension for the language used
according to this list:
\begin{itemize}
\item For Scala, \texttt{ext} is \texttt{sc}.
\item For Prolog, \texttt{ext} is \texttt{pl}.
\item For Ruby, \texttt{ext} is \texttt{rb}.
\item For Clojure, \texttt{ext} is \texttt{clj}.
\end{itemize}
If multiple languages are used in the homework,
submit a \texttt{hn.ext} file for each language.

\begin{center}
\textbf{If the language supports multiple different file extensions,}
\textbf{you must still follow the extension conventions above.}
\end{center}

\begin{center}
\textbf{Incorrect naming of files may result in up to a 10\% deduction in your grade.}
\end{center}

\subsubsection*{Do not submit testing or diagnostic code}
\label{sec:orgebb8991}

Unless you are instructed to do so in the homework questions,
\textbf{you should not submit testing code with your homework submission}.

This includes
\begin{itemize}
\item any \texttt{main} function,
\item any \texttt{print} statements which output information
\textbf{that is not directly requested as console output}
\textbf{in the homework questions}.
\end{itemize}

If you do not wish to remove diagnostic print statements manually,
you will have to find a way to ensure that they disabled
in your final submission.

For instance, by using a wrapper on the print function or macros.

\subsubsection*{Due date and allowance for technical difficulties}
\label{sec:org7c0639b}

Homework is due on the second Sunday following its release,
by the end of the day (midnight).
Submissions past 00:00 may not be considered.

If you experience technical difficulties
leading up to the submission time,
please contact Mark \textbf{ASAP} with the details of the problem
and, if possible, attach the current state of your homework
to the communication.
This information will help ensure we are able
to accept your submission once the technical difficulties are resolved.

\subsection*{Proper conduct for coursework}
\label{sec:org9790044}
\subsubsection*{Individual work}
\label{sec:orgc1b5e32}

Unless explicitely stated in the homework questions,
all homework in this course is intended
to be \emph{individually completed}.

You are welcome to discuss the content of the homework in
the public forum of the class Microsoft Teams team homework channel,
though obviously solutions or partial solutions should not
be posted or described.

Private discussions about the homework cannot reasonably be
forbidden, but such discussions should follow the same guidelines
as public discussions.

\begin{center}
\textbf{Inappopriate collaboration via private discussions}
\textbf{which is later discovered by course staff}
\textbf{may be considered academic dishonesty.}

When in doubt, make the discussion private, or report its contents
to the course staff by making a note of it
in your homework.
\end{center}

To clarify what is considered appropriate discussions
of homework content, here are some examples:
\begin{enumerate}
\item Discussing the language features introduced or needed for the homework.
\begin{itemize}
\item Such as relevant builtin datatypes
and datatype definition methods and their general use.
\item Code snippets that are not partial solutions to the homework
are welcome and encouraged.
\end{itemize}
\item Questions of the form “What is meant by \texttt{x}?”,
“Does \texttt{x} really mean \texttt{y}?” or “Is there a mistake with \texttt{x}?”
\begin{itemize}
\item Of course, questions of those form which would be answered
by partial solutions are not considered appropriate.
\end{itemize}
\item Questions or advice about errors that may be encountered.
\begin{itemize}
\item Such as “If you see a \texttt{scala.MatchError} you should
probably add a catch-all \texttt{\_} case to your \texttt{match} expressions.”
\end{itemize}
\end{enumerate}

\subsubsection*{Language library resources}
\label{sec:org1853d00}

Unless explicitely stated in the questions,
it is not expected that you will use any language library resources
in the homeworks.

Possible exceptions to this rule include implementations
of datatypes we discuss in this course, such as lists
or options/maybes, if they are included in a standard library
instead of being builtin.

\emph{Basic} operations on such types would also be allowed.
\begin{itemize}
\item For instance, \texttt{head}, \texttt{tail}, \texttt{append}, etc. on lists
would not require explicit permission to be used.
\item More complex operations such as sorting procedures
would require permission before you used them.
\end{itemize}

Additionally, the standard \emph{higher-order} operations
including \texttt{map}, \texttt{reduce}, \texttt{flatten}, and \texttt{filter} are permitted generally,
unless the task is to implement such a higher-order operator.

\section*{Part 0.1: “Naive” recursion vs. tail recursion}
\label{sec:orgce45ffb}
As discussed during the week 9 tutorials,
in general, we know that recursion is less efficient than
iteration (looping) as a control structure
due to the requirement of a \emph{stack frame} for each recursive call.

For instance, consider this “naively” defined factorial function.
\begin{minted}[breaklines=true]{clojure}
(defn factorial [n]
  (cond
    (= n 0) 1
    (> n 0) (* n (factorial (- n 1)))
    :else (throw (Exception. "Trying to calculate factorial of a negative number."))))
\end{minted}

If we “unwind the recursion” for a call to the above function,
we see that at each recursive step, there is more and more
“work to be done” added outside the recursive call.
When we do reach a base case, we then have to return back to each call,
doing the remaining work along the way.
\begin{minted}[breaklines=true]{text}
(factorial 3)
(* 3 (factorial 2))
(* 3 (* 2 (factorial 1)))
(* 3 (* 2 (* 1 (factorial 0))))
(* 3 (* 2 (* 1 1)))
(* 3 (* 2 1))
(* 3 2)
6
\end{minted}
This work to be done has to be remembered somehow,
and in principle this means that the stack frame for each call must
be maintained until that call returns.

In contrast to the “naive” approach above, a \emph{tail recursive} definition
can be constructed so that \emph{no work} remains to be done
upon returning from the recursive call.
Because of this fact, if the language implementation chooses to do so,
the stack frames can be reused, and we can return to the initial caller
when we reach the base case, instead of returning back through each
recursive caller.
\begin{minted}[breaklines=true]{clojure}
(defn factorial-tr [n]
  (defn fact-iter [n collect]
    (cond
      (= n 0) collect
      (> n 0) (fact-iter (- n 1) (* collect n))
      :else (throw (Exception. "Trying to calculate factorial of a negative number."))))
  (fact-iter n 1))
\end{minted}

If we visualise a call to this function, we can observe
that the calling context is constant (rather than growing in size),
implying that the memory requirements on the stack are also constant.
\begin{minted}[breaklines=true]{text}
(factorial-tr 3)
(fact-iter 3 1)
(fact-iter 2 3)
(fact-iter 1 6)
(fact-iter 0 6)
6
\end{minted}

Tail recursion is then more efficient than general recursion,
and is in fact equivalent to looping or iterating constructs.
The ideas behind an iterative implementation  and
a tail recursive implementation are also usually quite similar.

In this homework, we provide you with a new Clojure special form
which can be used to create functions
which automatically output a visualisation of their calling context,
and hence their stack usage, as they run.
You are tasked with using this new special form to implement
several simple recursive functions both in a “naive” way
and a tail recursive way.

\section*{Part 0.2: A new special form to automate visualisation of the stack}
\label{sec:orgb246cb9}
In this section, we define the new special form
used in this assignment, called “\texttt{unwindrec}”.

\begin{center}
\textbf{You may read about its definition if you are interested.}
\textbf{If you prefer though, skip to the next section,}
\textbf{\hyperref[sec:orgc0119f6]{Part 0.3: Using the new special form}
*which shows you how to use \texttt{unwindrec}.}

It's not required that you understand all the code in this section.
\end{center}

We need a way to replace a value in a string representation
of a Clojure term with a different value.
Specifically, this will be used to replace variable names with their values,
and to replace the placeholder \texttt{rec} with recursive calls
or the results of recursive calls.
\begin{minted}[breaklines=true]{clojure}
(defn replace-value-in-termstring
  "Replace all occurrences of `value` with `replacement`
within a `string` which is assumed to Clojure term.

Since the `string` is assumed to be a Clojure term,
this replaces the `value` only if it surrounded by spaces, parentheses,
braces or brackets (i.e., not if it is a substring of some subterm.)
One use of this is to replace a variable's name with its value,
as in `(replace-value-in-termstring 'var var string)`."
  [value replacement string]
  (clojure.string/replace
   string
   ;; The first group of this pattern matches the beginning of the string,
   ;; whitespace, or an opening parenthese/brace/bracket.
   ;; The last group matches the end of the string, whitespace, or
   ;; a closing parenthese/brace/bracket.
   (re-pattern (str "(\\A|\\s|\\[|\\{|\\()(" value ")(\\)|\\}|\\]|\\s|\\z)"))
   (str "$1" replacement "$3")))
\end{minted}

We actually need a version of the above which replaces multiple values.
\begin{minted}[breaklines=true]{clojure}
(defn replace-values-in-termstring
  "Replace all occurrences of the eleents of `values`
with the corresponding elements of `replacements`
within a `string` which is assumed to Clojure term.

This is the multiple replacement version of `replace-value-in-termstring`.
Note that values are replaced from in order,
and if an earlier replacement adds to the termstring
a value later in the list that value will also be replaced
(but not vice versa.)

Since the `string` is assumed to be a Clojure term,
this replaces the `value` only if it surrounded by spaces, parentheses,
braces or brackets (i.e., not if it is a substring of some subterm.)
One use of this is to replace a variable's name with its value,
as in `(replace-value-in-termstring 'var var string)`."
  [values replacements string]
  ;; `values` and `replacements` are assumed to be the same length,
  ;; but we check both are non-empty.
  (if (and (seq values) (seq replacements))
    ;; Deconstruct the lists.
    (let [[v & vs] values
          [r & rs] replacements]
      (replace-values-in-termstring vs rs
                                    (replace-value-in-termstring v r string)))
    ;; If `values` or `replacements` is empty, just return the string.
    string))
\end{minted}

We'll need to use the above for each element of the \texttt{args} list,
replacing each element with its current value,
and also replacing instances of the function name
(with \texttt{f} and the \texttt{context} argument.)

We'll also need to replace the recursive call(s) with
a placeholder before we pass it along to the recursive call,
so that the recursive call can tell where it was called from
in the string.

This method to find the first closing parenthese that is not matched
in a string will be necessary to tell where to replace up to.
I don't see a way that regular expressions could be used for this,
as there's no good way to specify “match up to
a closing parenthese \emph{that does not have an opening parenthese match}”.
This function instead uses a helper which keeps track of the number
of unmatched opening parentheses as we walk through the string.
\begin{minted}[breaklines=true]{clojure}
(defn closing-paren-in-string
  "Given a string `s`, return an two element list consisting of
the portion of `s` up to (and including) the first closing parenthese
that does not have a matching opening parenthese, and the remainder of `s`."
  [s]
  (letfn [(nth-closing-paren-in-string [s openings]
            (cond
              ;; If `s` has less than 2 characters, then even if it is a closing paren,
              ;; it's the first one and belongs in the first list.
              (> 2 (count s)) `(~(apply str s) "")
              :else
              ;; Otherwise, decompose `s` to check its first character.
              (let [[c & cs] s]
                (cond
                  ;; If it's an opening paren, increment `openings` in the recursive call
                  ;; and prepend `c` to the returned first string.
                  (= c \() (let [[before after] (nth-closing-paren-in-string cs (+ openings 1))]
                              `(~(str c before) ~(apply str after)))
                  ;; If it's a closing paren, either split the string here if `openngs` is 0,
                  ;; or decrement `openings` in the recursive call. And prepend `c` to the first string.
                  (= c \)) (if (= openings 0)
                             `(~(str c) ~(apply str cs))
                             (let [[before after] (nth-closing-paren-in-string cs (- openings 1))]
                                `(~(str c before) ~after)))
                  ;; Otherwise, just make the recursive call and prepend `c` to the returned first string.
                  :else     (let [[before after] (nth-closing-paren-in-string cs openings)]
                              `(~(str c before) ~after))))))]
    ;; Start out with 0 opening parentheses seen.
    (nth-closing-paren-in-string s 0)))
\end{minted}

Now, we can replace all instances of a call to a function
by splitting the string at that function name,
then further splitting the second part at
the first unmatched closing parenthese,
and replacing the chunk for the function call with a placeholder.
\begin{minted}[breaklines=true]{clojure}
(defn replace-call-in-termstring
  "Replace all calls to a function whose name is given by `f`
in a string representing "
  [f replacement string]
   ;; Find the first occurrence of `f` preceded by an opening parenthese.
   (let [m (re-find (re-pattern (str "(.*)\\((" f "\\s.*)")) string)]
     ;; The match m will be nil if no match was found. Otherwise,
     ;; because the pattern has two groups, it will be a vector
     ;; of the whole match, the portion before the `f` call,
     ;; and the `f` call and remainder of the string.
     ;; Note the opening parenthese is not in either group.
     (if m
       ;; Get the parts of m.
       (let [[whole before call-and-after] m
             ;; Separate `call-and-after` at the first unmatched closing parenthese.
             [callbody after] (closing-paren-in-string call-and-after)]
         (str before replacement after))
       ;; If m is null, just return the string as is.
       string)))
\end{minted}

Now we are finally ready to define our new special form.
It constructs a recursive function of the same form
we used for \texttt{factorial} and \texttt{factorial-tr},
carrying out the work of manipulating the “context string”
automatically.
\begin{minted}[breaklines=true]{clojure}
(defmacro unwindrec
  "Define a simple recursive function which prints out the
unwinding of the recursion as it runs.

The name of the function is given by `name`, and its arguments by `args`.

It is assumed that the function has a single base case,
guarded by condition `basecond` and with body `basebody`.
`basebody` should not contain any recursive calls;
the printing will not work properly otherwise.

It is further assumed that the function has a single recursive case,
guarded by condition `reccond` and with body `recbody`.
`recbody` should contain exactly one recursive call to `name`
somewhere in its body. The printing may not work properly otherwise.

The final, optional, argument `elsebody` is used as the body
if neither the `basecond` or `reccond` is satisfied.
"
  ([name args basecond basebody reccond recbody]
   ;; If `elsebody` is not provided, substitute `nil` instead.
   `(unwindrec ~name ~args ~basecond ~basebody ~reccond ~recbody nil))
  ([name args basecond basebody reccond recbody elsebody]
   ;; Define the function `name` taking arguments `args`.
   `(defn ~name ~args
      ;; This local function `f` actually implements `name`.
      ;; It has an additional argument, a string giving the context of
      ;; the call. This string should contain a substring "rec"
      ;; which is the point in the context at which the call was made.
      (letfn
          [(~'f [~'context ~@args]
            ;; Use the provided base case and recursive case conditions.
            (cond
              ;; In the base case, evaluate `basebody`. Substitute that value
              ;; for "rec" in the `context` and print the context,
              ;; then return the value.
              ~basecond (let [~'value ~basebody]
                          (println (replace-value-in-termstring
                                    '~'rec
                                    ~'value
                                    ~'context))
                          ~'value)
              ;; In the recursive case, replace "rec" in the `context`
              ;; with the recursive case body, and replace all argument names
              ;; with their current values.
              ~reccond  (let [~'this-context-with-call (replace-values-in-termstring
                                                        '~args
                                                        (map str ~args)
                                                        (replace-value-in-termstring
                                                         '~'rec
                                                         (str '~recbody)
                                                         ~'context))
                              ;; Also construct a new context `this-context-with-rec` to be
                              ;; passed in the recursive call by replacing
                              ;; the recursive call in `this-context-with-call` with "rec".
                              ~'this-context-with-rec (replace-call-in-termstring
                                                       '~name
                                                       "rec"
                                                       ~'this-context-with-call)]
                          (println ~'this-context-with-call)
                          ;; Now, actually make the recursive call, but first
                          ;; walk the `recbody` to replace `name` with `f` and
                          ;; the additional argument `this-context-with-rec`
                          ;; (remember that `f` is implementing `name`.)
                          (let [~'result ~(clojure.walk/prewalk-replace {name '(partial f this-context-with-rec)} recbody)
                                ~'this-context-with-result (replace-value-in-termstring '~'rec (str ~'result) ~'context)]
                            ;; Print out the context again with the result in place, unless
                            ;; the whole context is just the result (this is so
                            ;; just the result does not get printed over and over.)
                            (when (not= ~'this-context-with-result (str ~'result))
                              (println ~'this-context-with-result))
                            ~'result))
              ;; If neither `basecond` nor `reccond` was satisfied,
              ;; evaluate `elsebody`. This does no printing.
              :else ~elsebody))]

        ;; Before calling `f`, print out the originating call.
        (println (replace-values-in-termstring
                  '~args
                  (map str ~args)
                  (str "(" (clojure.string/join " " '(~name ~@args)) ")")))
        ;; Then call `f`.
        (~'f "rec" ~@args)))))
\end{minted}

\section*{Part 0.3: Using the new special form}
\label{sec:orgc0119f6}
Here, we show how to use this new special form
to define simple recursive functions such as factorial.

First, the “naive” recursive factorial.
\begin{minted}[breaklines=true]{clojure}
(unwindrec factorial [n]
            (= n 0) 1
            (> n 0) (* n (factorial (- n 1)))
            (throw (Exception. "Trying to calculate factorial of a negative number.")))
\end{minted}

And then the tail recursive variant. Note that we must again
wrap the helper function which takes two arguments in another \texttt{defn} that
defines the function taking one argument.
(In this case, the \texttt{factorial-tr-helper} function will be defined globally,
because \texttt{unwindrec} expands to a \texttt{defn} instead of a \texttt{letfn}, but that's okay.)
\begin{minted}[breaklines=true]{clojure}
(defn factorial-tr [n]
  (unwindrec factorial-tr-helper [n collect]
             (= n 0) collect
             (> n 0) (factorial-tr-helper (- n 1) (* collect n))
             (throw (Exception. "Trying to calculate factorial of a negative number.")))
  (factorial-tr-helper n 1))
\end{minted}

\section*{Part 1: Exponent              [10 marks]}
\label{sec:orgb915a74}
Similar to the \texttt{factorial} and \texttt{factorial-tr} functions defined
using the \texttt{unwindrec} form in \hyperref[sec:orgc0119f6]{Part 0.3},
define two functions \texttt{exponent} and \texttt{exponent-tr} which
implement the exponent/power function on natural numbers
(meaning that negative exponents are not supported.)

The full definitions of \texttt{unwindrec} and the helper functions it uses
can be found \href{./src/h8/unwindrec.clj}{here}. You may either copy the contents of this file into yours,
or import it, for instance by placing it in the same directory
as your \texttt{h8.clj} and writing \texttt{(load-file "./unwindrec.clj")} at the top of your file.

The \texttt{exponent} function should use “naive” recursion on the second argument,
whereas \texttt{exponent-tr} should use tail recursion (still on the second argument.)

For instance, \texttt{(exponent 2 3)} should return \texttt{8}, since \(2^{3} = 8\).

\section*{Part 2: Sum of a list         [10 marks]}
\label{sec:org46f7b54}
Now, use the \texttt{unwindrec} form to define functions \texttt{sumlist} and \texttt{sumlist-tr} which
sum up the elements of a list.

For instance, \texttt{(sumlist '(0 1 2 3))} should return \texttt{6}.

\section*{Part 3: Flatten a list        [20 marks]}
\label{sec:org2e9762f}
Use \texttt{unwindrec} again more to define functions \texttt{flattenlist} and \texttt{flattenlist-tr} which
flatten a list of lists to a list, by concatenating the lists.

For instance, \texttt{(flattenlist '((1 2) (3 4) (5 6 7 8)))} should return \texttt{(1 2 3 4 5 6 7 8)}.

\section*{Part 4: Postfixes of a list   [20 marks]}
\label{sec:org36488f0}
Use \texttt{unwindrec} once more to define functions \texttt{postfixes} and \texttt{postfixes-tr} which,
given a list \texttt{l}, return all the sublists which are postfixes of \texttt{l},
including \texttt{l} itself and the empty list, in decreasing order of length.

For instance, \texttt{(postfixes '(1 2 3 4 5 6))} should
return \texttt{((1 2 3 4 5 6) (2 3 4 5 6) (3 4 5 6) (4 5 6) (5 6) (6) ())}.

\textbf{Make sure that the empty list is considered a postfix}.

Note that you may need to write the list containing the empty list
as part of your code; this can be written \texttt{'(())}.

You may also need to write the list containing the value of a variable,
say for instance a variable \texttt{x}. You will find that
if you write \texttt{'(x)}, \texttt{x} is not evaluated and so is not replaced by its value.
Instead, you must write something like \texttt{`(\textasciitilde{}x)}. The backtick \texttt{`} is a special
kind of quote that allows for parts of the quoted expression,
marked with tildes \texttt{\textasciitilde{}}, to be evaluated.

\section*{Testing}
\label{sec:org5e0fae8}
Unit tests for the requested methods/functions
are available here \href{./testing/h8/h8t.clj}{h8t.clj}.
The contents of the unit test file are also repeated below.

\begin{center}
These tests confirm that your functions return the correct values.
However, they do not confirm that you have used the \texttt{unwindrec} form,
or that your naive and tail recursive definitions are indeed
naive and tail recursive respectively.
\end{center}

\subsection*{Automated testing via Docker}
\label{sec:org7ec87b6}
The Docker setup and usage scripts are available at the following links.
Their contents are also repeated below.
\begin{itemize}
\item \href{./testing/h8/Dockerfile}{Dockerfile}
\item \href{./testing/h8/docker-compose.yml}{docker-compose.yml}
\item \href{./testing/h8/setup.sh}{setup.sh}
\item \href{./testing/h8/run.sh}{run.sh}
\end{itemize}
Place them into your \texttt{h8} directory where your \texttt{h8.clj} file
and the \texttt{h8t.clj} (linked to above) files exist,
then run \texttt{setup.sh} and \texttt{run.sh}.

You may also refer to the README
for this testing setup and those files
\href{https://github.com/armkeh/principles-of-programming-languages/tree/master/homework/testing/h5}{on the course GitHub repo}.

Note that the use of the \texttt{setup.sh} and \texttt{run.sh} scripts assumes
that you are in a \texttt{bash} like shell; if you are on Windows,
and not using WSL or WSL2, you may have
to run the commands contained in those scripts manually.

\subsection*{The tests}
\label{sec:org37e50f0}
The contents of the testing script are repeated here.

\href{./testing/h8/h8t.clj}{h8t.clj}
\begin{minted}[breaklines=true]{clojure}
(ns testing)
(use 'clojure.test)
(load-file "h8.clj")

(deftest naive-exponent
  (is (= (exponent 1 10)  1))
  (is (= (exponent 2  5) 32))
  (is (= (exponent 5  2) 25)))

(deftest tailrec-exponent
  (is (= (exponent-tr 1 10)  1))
  (is (= (exponent-tr 2  5) 32))
  (is (= (exponent-tr 5  2) 25)))

(deftest naive-sumlist
  (is (= (sumlist '(0 1 2 3)) 6))
  (is (= (sumlist '(0 0 0 0)) 0))
  (is (= (sumlist '(10 -1 -1 -1 -1 -1)) 5)))

(deftest tailrec-sumlist
  (is (= (sumlist-tr '(0 1 2 3)) 6))
  (is (= (sumlist-tr '(0 0 0 0)) 0))
  (is (= (sumlist-tr '(10 -1 -1 -1 -1 -1)) 5)))

(deftest naive-flattenlist
  (is (= (flattenlist '((1 2) (3 4) (5 6) (7 8))) '(1 2 3 4 5 6 7 8)))
  (is (= (flattenlist '((0))) '(0)))
  (is (= (flattenlist '((1) ((2) (3)))) '(1 (2) (3)))))

(deftest tailrec-flattenlist
  (is (= (flattenlist-tr '((1 2) (3 4) (5 6) (7 8))) '(1 2 3 4 5 6 7 8)))
  (is (= (flattenlist-tr '((0))) '(0)))
  (is (= (flattenlist-tr '((1) ((2) (3)))) '(1 (2) (3)))))

(deftest naive-postfixes
  (is (= (postfixes '(1 2 3 4 5 6)) '((1 2 3 4 5 6) (2 3 4 5 6) (3 4 5 6) (4 5 6) (5 6) (6) ())))
  (is (= (postfixes '(0)) '((0) ())))
  (is (= (postfixes '(6 5 4 3 2 1)) '((6 5 4 3 2 1) (5 4 3 2 1) (4 3 2 1) (3 2 1) (2 1) (1) ()))))

(deftest tailrec-postfixes
  (is (= (postfixes-tr '(1 2 3 4 5 6)) '((1 2 3 4 5 6) (2 3 4 5 6) (3 4 5 6) (4 5 6) (5 6) (6) ())))
  (is (= (postfixes-tr '(0)) '((0) ())))
  (is (= (postfixes-tr '(6 5 4 3 2 1)) '((6 5 4 3 2 1) (5 4 3 2 1) (4 3 2 1) (3 2 1) (2 1) (1) ()))))

;; If we define `test-ns-hook`, it is called when running `run-tests`,
;; instead of just calling all tests in the namespace.
;; This lets us control the order of the tests.
(deftest test-ns-hook
  (naive-exponent)
  (naive-sumlist)
  (naive-flattenlist)
  (naive-postfixes)
  (tailrec-exponent)
  (tailrec-sumlist)
  (tailrec-flattenlist)
  (tailrec-postfixes))

(run-tests 'testing)
\end{minted}

\subsection*{The Docker setup}
\label{sec:org7c81111}
The contents of the Docker setup files and scripts are repeated here.

\href{./testing/h7/Dockerfile}{Dockerfile}
\begin{minted}[breaklines=true]{docker}
# Define the argument for openjdk version
ARG OPENJDK_TAG=8u232

FROM clojure:openjdk-8
    
# Set the name of the maintainers
MAINTAINER Habib Ghaffari Hadigheh, Mark Armstrong <ghaffh1@mcmaster.ca, armstmp@mcmaster.ca>

# Set the working directory
WORKDIR /opt/h8
\end{minted}

\href{./testing/h7/docker-compose.yml}{docker-compose.yml}
\begin{minted}[breaklines=true]{yaml}
version: '2'
services:
  service:
    build: .
    image: 3mi3_h8_docker_image
    volumes:
      - .:/opt/h8
    container_name: 3mi3_h8_container
    command: bash -c
      "cat h8t.clj | lein repl"
\end{minted}

\href{./testing/h5/setup.sh}{setup.sh}
\begin{minted}[breaklines=true]{shell}
docker-compose build --force-rm
\end{minted}

\href{./testing/h5/run.sh}{run.sh}
\begin{minted}[breaklines=true]{shell}
# Run the container
docker-compose up --force-recreate
# Stop the container after finishing the test run
docker-compose stop -t 1
\end{minted}

\href{./testing/h5/README.md}{README.md}
\begin{minted}[breaklines=true]{text}
# Instructions for automated testing using Docker

We have already created a `Dockerfile` here which specifies
all the necessary packages, etc., for compiling and running your code.
You only need to follow the instructions below to see 
the results of unit tests designed to check your implementation.

## Setup
We use `docker-compose` and its configuration file to build the image.
Assuming you have `docker` and `docker-compose` installed,
simply execute
```shell script
./setup.sh
```
to generate the image.

## Prepare your code for the running the tests
You only need to place the `h8t.clj` unit test file and
the `run.sh` file in the same directory as your `h8.clj` source file.

## Running the tests
As with the build process, we have already put
the configuration needed for running the test inside `docker-compose.yml`.
Simply execute
```shell script
./run.sh
```
to run and see the results of the tests.
\end{minted}
\end{document}
